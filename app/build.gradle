version = '0.7.0'

apply plugin: 'com.android.application'


android {
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("$rootProject.projectDir/$rootProject.testUserKeyStorePath",)
            storePassword rootProject.keyStorePassword
            keyAlias rootProject.signKeyAlias
            keyPassword rootProject.keyStorePassword
        }
    }

    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 22
        multiDexEnabled true
    }

    buildTypes {
        debug { }
        release {
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk') && !outputFile.name.contains("debug")) {
                copy {
                    from output.outputFile
                    into projectDir
                    rename (outputFile.name, "${signedAndroidAppName}.apk")
                }
                println "copied apk into ${projectDir}"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    compile 'com.android.support:multidex:1.0.0'
    compile ('org.apache.httpcomponents:httpmime:4.2.2') {
        exclude module: 'httpcore'
    }
    compile 'com.android.support:appcompat-v7:22.1.0'
    compile 'com.android.support:cardview-v7:22.1.0'
    compile 'com.android.support:recyclerview-v7:22.1.0'
    compile 'com.google.zxing:android-core:3.1.0'
    compile 'com.google.zxing:core:3.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.2'
    compile 'org.votingsystem.android:bouncycastle:0.7.0-SNAPSHOT'

    //BUG with Android 5.0 and Tyrus client!!! WSS connections throw SSLHandshakeException
    //https://java.net/projects/tyrus/lists/users/archive/2015-01/message/0
    compile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.10'

}

preBuild.dependsOn 'setAndroidEnv'


task setAndroidEnv() << {
    //env.each{println it}
    String androidHome= System.getenv()['ANDROID_HOME']
    if(!androidHome) throw new RuntimeException('Android SDK not Found you must install -> http://developer.android.com/sdk/index.html')
}
